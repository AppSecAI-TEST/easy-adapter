apply plugin: 'android-library'
apply plugin: 'maven'
apply plugin: 'signing'

version = "1.0.1"
group = "uk.co.ribot"

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar', '*.aar'])
}

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.3"

    defaultConfig {
        minSdkVersion 7
        targetSdkVersion 19
    }
}

task androidJavadocs(type: Javadoc) {
    options.links("http://docs.oracle.com/javase/7/docs/api/");
    source = android.sourceSets.main.allJava
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.allSource
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

configurations {
    archives {
        extendsFrom configurations.default
    }
}

signing {
    required { has("release") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

//Generates JAR file when building
android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name.equals(com.android.builder.BuilderConstants.DEBUG)) {
        return;
    }
    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir
    artifacts.add('archives', task);
}

uploadArchives {
    configuration = configurations.archives
    repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: sonotypeRepoSnapshots) {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            name 'Android Easy Adapter'
            packaging 'aar'
            description 'Library that facilitates the use of AdapterViews on Android'
            url 'https://github.com/ribot/easy-adapter'

            scm {
                url 'git@github.com:ribot/easy-adapter.git'
                connection 'scm:git:git@github.com:ribot/easy-adapter.git'
                developerConnection 'scm:git:git@github.com:ribot/easy-adapter.git'
            }

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }

            developers {
                developer {
                    id 'ivancarballo'
                    name 'Ivan Carballo'
                    email 'ivan@ribot.co.uk'
                }
            }
        }
    }
}
